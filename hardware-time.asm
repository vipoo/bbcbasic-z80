;	TITLE	BBC BASIC (C) R.T.RUSSELL 1987
;
;BBC BASIC INTERPRETER - Z80 VERSION
;HARDWARE SOUND INTERFACE
;(C) COPYRIGHT  R.T.RUSSELL  1984
; extensions by Dean Netherton 2020

include "hbios.inc"
include "constants.inc"

	PUBLIC	GETIME
	PUBLIC	PUTIME
	PUBLIC	TIMETICK
	PUBLIC	TIMETICKQUICK

	EXTERN	FMUL
	EXTERN	FDIV
	EXTERN	INT
	EXTERN	SFLOAT
	EXTERN	COPY
	EXTERN	SWAP

TIMETICKQUICK:
	RET

TIMETICK:
	RET

; Read elapsed-time clock.
;	Outputs: DEHL = elapsed time (centiseconds)
;	Destroys: A,D,E,H,L,F
GETIME:
	LD	BC, SYSGET_TIMER
	CALL_HBIOS	; DE:HL
	PUSH	BC		; CAPTURE TICK FREQUENCY

	CALL	DEHL_TO_INT
	CALL	SFLOAT
	CALL	COPY		; DED'E'B = HLH'L'C

	EXX
	POP	HL
	LD	H, 0		; RESTORE TICK FREQUENCY
	EXX
	LD	HL, 0
	LD	C, 0
	CALL	SFLOAT

	CALL	SWAP
	CALL	FDIV
	CALL	COPY		; DED'E'B = HLH'L'C

	EXX
	LD	HL, 100		; MULT BY 100
	EXX
	LD	HL, 0
	LD	C, 0
	CALL	SFLOAT
	CALL	FMUL
	CALL	INT

	CALL	INT_TO_DEHL
	RET

; Load elapsed-time clock.
;	Inputs: DEHL = time to load (centiseconds)
; 	Destroys: A,D,E,H,L,F
PUTIME:
	CALL	DEHL_TO_INT
	CALL	SFLOAT
	CALL	COPY		; DED'E'B = HLH'L'C

	EXX
	LD	HL, 100		; DIVIDE BY 100
	EXX
	LD	HL, 0
	LD	C, 0
	CALL	SFLOAT

	CALL	SWAP
	CALL	FDIV
	CALL	COPY		; DED'E'B = HLH'L'C

	EXX
	LD	HL, 60		; MULT BY 60
	EXX
	LD	HL, 0
	LD	C, 0
	CALL	SFLOAT
	CALL	FMUL
	CALL	INT
	CALL	INT_TO_DEHL

	LD	BC, SYSSET_TIMER
	CALL_HBIOS
	RET

;
; CONVERT 32 BIT INT IN DE:HL TO HLH'L'C
DEHL_TO_INT:
	PUSH	HL
	EX	DE, HL
	EXX
	POP	HL
	EXX
	LD	C, 0
	RET

;
; CONVERT 32 BIT INT IN HLH'L'C TO DE:HL
INT_TO_DEHL:
	EXX
	PUSH	HL
	EXX
	EX	DE, HL
	POP	HL
	RET